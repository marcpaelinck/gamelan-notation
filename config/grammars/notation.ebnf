@@grammar :: NOTATION 

@@whitespace :: /[ ]+/ 
@@parseinfo :: True

start
    =
    notation $
    ;

nobrace = /[^\{\}]+/ ;
eol = /\n/ | /\r\n/ | $ ;
tab = /\t/ ;
emptyline = {tab} eol;
nonspacechars = /[^\s]*/ ;
comment = /[^\r\n]+/ ; 

(* Notation can start with one unbound group of metadata lines.
   All other metadata lines should either immediately precede or
   immediately follow a gongan *)

notation =  {emptyline}*
            unbound:(unbound_elements | {})
            gongans:{gongan}+
            ;

unbound_elements =  @:{unbound_line}+
                    {emptyline}+
                    ;

gongan = @:{gongan_line}+
         ({emptyline}+ | {emptyline}* $)
                 ;

unbound_line = metadata:metadata_line | comments:comment_line;

(* using plurals 'staves' and 'comments' because they will be grouped during post-processing *)
gongan_line = metadata:metadata_line | staves:stave_line | comments:comment_line;

(* metadata is defined in metadata.ebnf *)
metadata_line = {"metadata"} tab "{" ~ @:metadata "}" {tab} eol ;

comment_line = (("comment" tab) | ({tab} "#")) ~ @:comment {tab} eol ;

stave_line = position:tag beats:{beat_with_tab} {tab} eol ;

(* position tag may be anything except 'comment' or 'metadata' and can be optionally followed 
   by a pass indicator: a colon followed by digits or by two groups of digits separated by a hyphen.
   The latter option indicates a range of passes (e.g. `:1-3`).
   The validity of the position and pass values will be checked in the post processing code.
   Note that the regex for the pass indicator allows for a hypen without a second digit. 
   This (incorrect) case will also be caught in the post processing code.
   If no pass value is encountered the default -1 is returned                          *)
(*tag = tag:/(?!comment|metadata)\w[\w\- \/]*/ pass:(/:(\d+)/ | `-1`); *)
tag = tag:/(?!comment|metadata\t)\w[\w\ \/\+]*/ pass:(/:(\d+-?\d*)/ | `-1`);

(* `note` is defined in font5.metadata *)
beat_with_tab = tab @:{note};

#include :: "./gamelan-notation/config/grammars/metadata.ebnf"
#include :: "./gamelan-notation/config/grammars/font5.ebnf"
