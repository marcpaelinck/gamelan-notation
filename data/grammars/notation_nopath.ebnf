@@grammar :: NOTATION 

@@whitespace :: /[ ]+/ 
@@parseinfo :: False

start
    =
    notation $
    ;

nobrace = /[^\{\}]+/ ;
eol = /\n|\r\n/ ;
tab = /\t/ ;
emptyline = /\t*(\n|\r\n)/;
nonspacechars = /[^\s]*/ ;
anychar = /.+/ ;

(* Notation can start with one unbound group of metadata lines.
   All other metadata lines should either immediately precede or
   immediately follow a gongan *)

notation =  {emptyline}*
            unbound:(unbound_elements | {})
            gongans:{gongan}+
            ;

unbound_elements =  @:{unbound_line}+
                    {emptyline}+
                    ;

gongan = {regular_gongan} final_gongan ;

regular_gongan = @:{gongan_line}+
                 {emptyline}+
                 ;

final_gongan =  @+:{gongan_line}+
                {emptyline}*
                ;

unbound_line = metadata:metadata_line | comment:comment_line;

gongan_line = metadata:metadata_line | stave:stave_line | comment:comment_line;

(* metadata is defined in metadata.ebnf*)
metadata_line = "metadata" ~ tab "{" @:metadata "}" {tab} eol ;

comment_line = "comment" ~ tab @:anychar eol ;

stave_line = position:tag beats:{beat_with_tab} {tab} eol ;

(* position tag may be anything except 'comment' or 'metadata' (no check on value currently) *)
tag = /(?!comment|metadata)\w[\w\- ]*/ ;

(* beat is defined in font5.metadata *)
beat_with_tab = tab @:beat ;

#include :: "font5.ebnf"
#include :: "metadata.ebnf"
